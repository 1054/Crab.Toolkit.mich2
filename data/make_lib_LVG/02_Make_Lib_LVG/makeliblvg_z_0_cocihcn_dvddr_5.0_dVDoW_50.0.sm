makeliblvg
    # 
    # run radex and make lib.lvg
    # 
    # Usage:
    #     macro read makeliblvg_z_0_cocihcn_dvddr_5.0_dVDoW_50.0.sm makeliblvg
    # 
    # Note:
    #     please note these:
    #         define TbCMB 6.89  # <TODO> T_{CMB} at z~1.5 see Dannerbauer 2009 BzK
    # 
    #set dirMKLIB = {"/Users/dliu/Working/SpireLines/Tool/Level_3_SciData/06_LVG_Synthesis/02_Make_Lib_LVG"}
    set dirMKLIB = {"/Users/dzliu/Cloud/Github/Crab.Toolkit.michi2/data/make_lib_LVG/02_Make_Lib_LVG"}
    set dirRADEX = {"/Users/dzliu/Cloud/Github/Crab.Toolkit.michi2/data/make_lib_LVG/01_RadiativeTransferCode/RADEX"}
    # 
    set Tk = 5.0,500.,25
    set nH = 2.0,6.5,0.1
    # 
    !echo -n "Will make "$(dimen(Tk)*dimen(nH)*16)' items for lib.lvg, '
    define OK ?
    if(!$OK){
        echo Abort by user. 
        return
    }
    # 
    # constants 
    set k_B = 1.380658e-16 # Boltzmann constant CGS erg K-1
    set h_0 = 6.6260755e-27 # Planck constant CGS erg s
    set v_c = 2.99792458e10 # cm/s
    set g_0 = 6.672e-11
    set tkc = 2.0*k_B/v_c/v_c
    # 
    CHDIR $(dirRADEX)
    !date +"\#\ %Y-%m-%d\ %H:%M:%S" > "lib.lvg"
    print +"lib.lvg" '\# NVAR  = 2 \# Variable X Y\n' {}
    print +"lib.lvg" '\# TVAR1 = J_{upper}\n' {}
    print +"lib.lvg" '\# TVAR2 = S_{CO}*(1+z)^3/Ω_S, in unit of Jy km/s ' {}
    print +"lib.lvg" '\# Ω_S == π * r_S^2 / d_A^2, in unit of sr\n' {}
    print +"lib.lvg" '\# CVAR1 = 1\n' {}
    print +"lib.lvg" '\# CVAR2 = 14 \# FLUX_Jykms\n' {}
    print +"lib.lvg" '\# NVAR1 = 25 \# CO 16 lines, [CI] 3 lines, HCN 6 lines.\n' {}
    print +"lib.lvg" '\# NVAR2 = $(dimen(Tk)*dimen(nH)) \# \n' {}
    print +"lib.lvg" '\# NPAR  = 2 \# Parameter 1 2 3 ...\n' {}
    print +"lib.lvg" '\# TPAR1 = T_{kin}\n' {}
    print +"lib.lvg" '\# TPAR2 = n_{H_2}\n' {}
    print +"lib.lvg" '\# NPAR1 = $(dimen(Tk))\n' {}
    print +"lib.lvg" '\# NPAR2 = $(dimen(nH))\n' {}
    print +"lib.lvg" '\# CPAR1 = 5 \# T_Kin\n' {}
    print +"lib.lvg" '\# CPAR2 = 6 \# n_H_2\n' {}
    print +"lib.lvg" '\# \n' {}
    print +"lib.lvg" '\# rest_freq   species   J_upper   J_lower' {}
    print +"lib.lvg" '      T_Kin            n_H_2        pop_upper        pop_lower              tau' {}
    print +"lib.lvg" '             T_ex             T_RJ        FLUX_Kkms        FLUX_ergs       FLUX_Jykms' {}
    print +"lib.lvg" '\n' {}
    define ii 0
    foreach TkOne Tk {
        foreach nHOne nH {
            define dvddr 5.0   # <TODO> well, not sure! Galactic SF nuclei typical (dV/dR) ∼ (2–6) km s−1 pc−1 (Weiss et al. 2001)
            define dVDoW 50.0  # <TODO> well, not sure! 50.0/5.0 = 10 pc, that is molecular cloud size. 
            define NXAbD 3.2e-4  # <TODO> CO Abundance, X[CO]/X[H2] ≈ 3.2e-4 in MW (Sofia et al. 2004). Papadopoulos+2012 adopted [CO/H2] = 1e-4. Kamenetzky+2015 adopted [CO/H2] = 3e-4. But X[CO]/X[H2]/(dV/dr) = NXAbD/dvddr = 1e-5 is adopted by Dannerbauer 2009, see their Sec.4, see also Weiss 2005 M82 range 1E-6 to ?E-5
            define NHLoS $($dVDoW/$dvddr*(10**$nHOne)*3.086e18)
            define NXLoS $($NHLoS*$NXAbD)
            define TbCMB 2.72548  # <TODO> T_{CMB} at z~0! Fixsen 2009 ApJ
            print  "inp/co.inp" './data/co.dat\n' {}
            print +"inp/co.inp" './out/co_$TkOne"_"$nHOne.out\n' {}
            print +"inp/co.inp" '100 1900\n' {} # Freq Range 
            print +"inp/co.inp" '$TkOne\n' {} # Tk
            print +"inp/co.inp" '2\n' {}
            print +"inp/co.inp" 'o-H2\n' {}
            print +"inp/co.inp" '$(0.75*(10**$nHOne))\n' {} # 0.75 ortho-H2 see Dannerbauer 2009 BzK
            print +"inp/co.inp" 'p-H2\n' {}
            print +"inp/co.inp" '$(0.25*(10**$nHOne))\n' {} # 0.25 para-H2 see Dannerbauer 2009 BzK
            print +"inp/co.inp" '$TbCMB\n' {} # T_{CMB} at z~1.5 Dannerbauer
           #print +"inp/co.inp" '$NHLoS\n' {} # Column H2 Density along Line of Sight -- NHLoS
            print +"inp/co.inp" '$NXLoS\n' {} # Column CO Density along Line of Sight -- NXLoS
            print +"inp/co.inp" '$dVDoW\n' {} # Velocity Corresponding to Doppler Width -- dVDoW
            print +"inp/co.inp" '0\n' {}
            !./bin/radex < inp/co.inp > out/co_$TkOne"_"$nHOne.log
            
            define NXAbD 2.2e-5  # <TODO> CI Abundance 3e-5, Weiß+2003, Stutzki+1997, Walter+2002 and Weiß+2001. But X[CI]/X[H2] = 2.2e-5 in MW (Frerking et al. 1989). 
            define NHLoS $($dVDoW/$dvddr*(10**$nHOne)*3.086e18)
            define NXLoS $($NHLoS*$NXAbD)
            print  "inp/ci.inp" './data/catom.dat\n' {}
            print +"inp/ci.inp" './out/ci_$TkOne"_"$nHOne.out\n' {}
            print +"inp/ci.inp" '100 1900\n' {} # Freq Range 
            print +"inp/ci.inp" '$TkOne\n' {} # Tk
            print +"inp/ci.inp" '2\n' {}
            print +"inp/ci.inp" 'o-H2\n' {}
            print +"inp/ci.inp" '$(0.75*(10**$nHOne))\n' {} # 0.75 ortho-H2 see Dannerbauer 2009 BzK
            print +"inp/ci.inp" 'p-H2\n' {}
            print +"inp/ci.inp" '$(0.25*(10**$nHOne))\n' {} # 0.25 para-H2 see Dannerbauer 2009 BzK
            print +"inp/ci.inp" '$TbCMB\n' {} # T_{CMB} at z~1.5 Dannerbauer
           #print +"inp/ci.inp" '$NHLoS\n' {} # Column H2 Density along Line of Sight -- NHLoS
            print +"inp/ci.inp" '$NXLoS\n' {} # Column CO Density along Line of Sight -- NXLoS
            print +"inp/ci.inp" '$dVDoW\n' {} # Velocity Corresponding to Doppler Width -- dVDoW
            print +"inp/ci.inp" '0\n' {}
            !./bin/radex < inp/ci.inp > out/ci_$TkOne"_"$nHOne.log
            
            define NXAbD 5e-9  # <TODO> HCN Abundance, Knudsen+2007ApJ...666..156K adopted [HCN/H2] = 5e-9 and [HCO+/H2] = 1.6e−9 determined for NGC 253 by Martín et al. (2006) and a velocity gradient of 8 kms−1 pc−1. Rosolowsky+2011 adopted 1.4e-9 for M33 (significantly lower metallicty). 
            define NHLoS $($dVDoW/$dvddr*(10**$nHOne)*3.086e18)
            define NXLoS $($NHLoS*$NXAbD)
            print  "inp/hcn.inp" './data/hcn.dat\n' {}
            print +"inp/hcn.inp" './out/hcn_$TkOne"_"$nHOne.out\n' {}
            print +"inp/hcn.inp" '80 600\n' {} # Freq Range 
            print +"inp/hcn.inp" '$TkOne\n' {} # Tk
            print +"inp/hcn.inp" '2\n' {}
            print +"inp/hcn.inp" 'o-H2\n' {}
            print +"inp/hcn.inp" '$(0.75*(10**$nHOne))\n' {} # 0.75 ortho-H2 see Dannerbauer 2009 BzK
            print +"inp/hcn.inp" 'p-H2\n' {}
            print +"inp/hcn.inp" '$(0.25*(10**$nHOne))\n' {} # 0.25 para-H2 see Dannerbauer 2009 BzK
            print +"inp/hcn.inp" '$TbCMB\n' {} # T_{CMB} at z~1.5 Dannerbauer
           #print +"inp/hcn.inp" '$NHLoS\n' {} # Column H2 Density along Line of Sight -- NHLoS
            print +"inp/hcn.inp" '$NXLoS\n' {} # Column CO Density along Line of Sight -- NXLoS
            print +"inp/hcn.inp" '$dVDoW\n' {} # Velocity Corresponding to Doppler Width -- dVDoW
            print +"inp/hcn.inp" '0\n' {}
            !./bin/radex < inp/hcn.inp > out/hcn_$TkOne"_"$nHOne.log
            
            # combine CO CI HCN
            !cat "out/co_"$TkOne"_"$nHOne".out"  | head -n 13  | tail -n 2        | sed -e 's/\$/   SPECIES/g'  >  "out/combined_"$TkOne"_"$nHOne".out"
            !cat "out/co_"$TkOne"_"$nHOne".out"  | tail -n +14 | sed -e 's/--//g' | sed -e 's/\$/   CO/g'       >> "out/combined_"$TkOne"_"$nHOne".out"
            !cat "out/ci_"$TkOne"_"$nHOne".out"  | tail -n +14 | sed -e 's/--//g' | sed -e 's/\$/   \[CI\]/g'   >> "out/combined_"$TkOne"_"$nHOne".out"
            !cat "out/hcn_"$TkOne"_"$nHOne".out" | tail -n +14 | sed -e 's/--//g' | sed -e 's/\$/   HCN/g'      >> "out/combined_"$TkOne"_"$nHOne".out"
            
            #exit
            
            # 
            data "out/combined_"$TkOne"_"$nHOne".out" lines 3 0 read {J_UP 1 J_LOW 2 FREQ 4 T_EX 6 TAU 7 T_RJ 8 POP_UP 9 POP_LOW 10 FLUX_Kkms 11 FLUX_ergs 12 SPECIES 13.s}
            set Species = (FREQ>0) ? 'CO' : 'unknown'
            set TkArr = T_RJ*0.0+$TkOne
            set nHArr = T_RJ*0.0+(10**$nHOne)
            set emArr = T_RJ*0.0+0.0
            set FLUX_Jykms = T_RJ * tkc * (FREQ*1e9)**2 * 1.0645*$dVDoW * 1e23 # T_RJ * 2 k nu^2/c^2 * DeltaV
                                                                               # in unit of Jy km/s
                                                                               # 1.0645 see radex_manual.pdf sec.4.3
            # 
            define print_noheader 1
            print +"lib.lvg" '%11.4f %9s %9.0f %9.0f %10g %16e %16e %16e %16e %16e %16e %16e %16e %16e\n' \
                             {FREQ SPECIES J_UP J_LOW TkArr nHArr POP_UP POP_LOW TAU T_EX T_RJ FLUX_Kkms FLUX_ergs FLUX_Jykms}
        }
    }
    !pwd
    CHDIR $(dirMKLIB)
    !pwd
    # 
    # copy
    !cp $(dirRADEX)/lib.lvg lib_z_0_cocihcn_dvddr_5.0_dVDoW_50.0.lvg
    # 
    # end
