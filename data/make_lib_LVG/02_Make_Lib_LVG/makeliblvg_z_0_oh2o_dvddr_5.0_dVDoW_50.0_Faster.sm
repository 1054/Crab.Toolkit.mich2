makeliblvg
    # 
    # run radex and make lib.lvg for z=0 h2o
    # 
    # Usage:
    #     macro read makeliblvg_z_0_oh2o_dvddr_5.0_dVDoW_50.0_Faster.sm makeliblvg
    # 
    # Note:
    #     please note these:
    #         define TbCMB 6.89  # <TODO> T_{CMB} at z~1.5 see Dannerbauer 2009 BzK
    # 
    # Note: 
    #     additional note:
    #         after making the liblvg, please check the lib.lvg, 
    #         in case there are some ' *'s inside, 
    #         we can replace ' *' with '0'
    # 
    set dirMKLIB = {".."}
    set dirRADEX = {"radex"}
    # 
    set Tk = 5.0,300.,25
    set nH = 2.0,6.5,0.2
    # 
    !echo -n "Will make "$(dimen(Tk)*dimen(nH)*16)' items for lib.lvg, '
    define OK ?
    if(!$OK){
        echo Abort by user. 
        return
    }
    # 
    # constants 
    set k_B = 1.380658e-16 # Boltzmann constant CGS erg K-1
    set h_0 = 6.6260755e-27 # Planck constant CGS erg s
    set v_c = 2.99792458e10 # cm/s
    set g_0 = 6.672e-11
    set tkc = 2.0*k_B/v_c/v_c
    # 
    CHDIR $(dirRADEX)
    !date +"\#\ %Y-%m-%d\ %H:%M:%S" > "lib.lvg"
    print +"lib.lvg" '\# NVAR  = 2 \# Variable X Y\n' {}
    print +"lib.lvg" '\# TVAR1 = J_{upper}\n' {}
    print +"lib.lvg" '\# TVAR2 = S_{H2O}*(1+z)^3/Ω_S, in unit of Jy km/s ' {}
    print +"lib.lvg" '\# Ω_S == π * r_S^2 / d_A^2, in unit of sr\n' {}
    print +"lib.lvg" '\# CVAR1 = 3\n' {} # column number of VAR1
    print +"lib.lvg" '\# CVAR2 = 12\n' {} # column number of VAR2
    print +"lib.lvg" '\# NVAR1 = 24 \# ortho-H2O has 24 transitions within 500-2000 GHz\n' {}
    print +"lib.lvg" '\# NVAR2 = $(dimen(Tk)*dimen(nH)) \# \n' {}
    print +"lib.lvg" '\# NPAR  = 2 \# Parameter 1 2 3 ...\n' {}
    print +"lib.lvg" '\# TPAR1 = T_{kin}\n' {}
    print +"lib.lvg" '\# TPAR2 = n_{H_2}\n' {}
    print +"lib.lvg" '\# NPAR1 = $(dimen(Tk))\n' {}
    print +"lib.lvg" '\# NPAR2 = $(dimen(nH))\n' {}
    print +"lib.lvg" '\# CPAR1 = 4\n' {} # column number of PAR1
    print +"lib.lvg" '\# CPAR2 = 5\n' {} # column number of PAR1
    print +"lib.lvg" '\# \n' {}
    print +"lib.lvg" '\# J_upper   J_lower      FREQ      T_Kin            n_H_2    Null1    Null2    Null3' {}
    print +"lib.lvg" '              tau             T_ex             T_RJ        S_H2O_1zO\n' {}
    define ii 0
    foreach TkOne Tk {
        foreach nHOne nH {
            define dvddr 5.0   # <TODO> well, not sure!
            define dVDoW 50.0  # <TODO> well, not sure!
            define NXAbD $(1e-6/4*3)  # <TODO> ortho-H2O Abundance, X(oH2O)=1e-6/4*3, Gonzalez-Alfonso(2014,A&A,567,A91)
            define NHLoS $($dVDoW/$dvddr*(10**$nHOne)*3.086e18)
            define NXLoS $($NHLoS*$NXAbD)
            define TbCMB 2.72548  # <TODO> T_{CMB} at z~0! Fixsen 2009 ApJ
            print  "inp/h2o.inp" './data/oh2o@daniel.dat\n' {} # <TODO> h2o.dat
            print +"inp/h2o.inp" './out/oh2o_$TkOne"_"$nHOne.out\n' {}
            print +"inp/h2o.inp" '500 2000\n' {} # Freq Range -- <TODO> must match NVAR1 <TODO>
            print +"inp/h2o.inp" '$TkOne\n' {} # Tk
            print +"inp/h2o.inp" '2\n' {}
            print +"inp/h2o.inp" 'o-H2\n' {}
            print +"inp/h2o.inp" '$(0.75*(10**$nHOne))\n' {} # 0.75 ortho-H2 see Dannerbauer 2009 BzK
            print +"inp/h2o.inp" 'p-H2\n' {}
            print +"inp/h2o.inp" '$(0.25*(10**$nHOne))\n' {} # 0.25 para-H2 see Dannerbauer 2009 BzK
            print +"inp/h2o.inp" '$TbCMB\n' {} # T_{CMB} at z~1.5 Dannerbauer
           #print +"inp/h2o.inp" '$NHLoS\n' {} # Column H2 Density along Line of Sight -- NHLoS
            print +"inp/h2o.inp" '$NXLoS\n' {} # Column CO Density along Line of Sight -- NXLoS
            print +"inp/h2o.inp" '$dVDoW\n' {} # Velocity Corresponding to Doppler Width -- dVDoW
            print +"inp/h2o.inp" '0\n' {}
            !./bin/radex < inp/h2o.inp > out/oh2o_$TkOne"_"$nHOne.log
            # 
            data "out/oh2o_"$TkOne"_"$nHOne".out" lines 14 0 read {J_UP 1.s J_LOW 2.s FREQ 4 T_EX 6 TAU 7 T_RJ 8 FLUX1 11}
            set TkArr = T_RJ*0.0+$TkOne
            set nHArr = T_RJ*0.0+(10**$nHOne)
            set emArr = T_RJ*0.0+0.0
            set S_H2O = T_RJ * tkc * (FREQ*1e9)**2 * 1.0645*$dVDoW * 1e23 # T_RJ * 2 k nu^2/c^2 * DeltaV
                                                                         # in unit of Jy km/s
                                                                         # 1.0645 see radex_manual.pdf sec.4.3
            # 
            # make H2O line FREQ code
            set FREQ = int(FREQ+0.5)
            # 
            define print_noheader 1
            print +"lib.lvg" '%9s %9s %9d %10g %16e %8g %8g %8g %16e %16e %16e %16e\n' \
                             {J_UP J_LOW FREQ TkArr nHArr emArr emArr emArr TAU T_EX T_RJ S_H2O}
        }
    }
    !pwd
    CHDIR $(dirMKLIB)
    !pwd
    # 
    # copy
    !cp $(dirRADEX)/lib.lvg lib_z_0_oh2o_dvddr_5.0_dVDoW_50.0_Faster.lvg
    # 
    # end
