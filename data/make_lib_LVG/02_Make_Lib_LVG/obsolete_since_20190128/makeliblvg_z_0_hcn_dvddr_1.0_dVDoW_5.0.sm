makeliblvg
    # 
    # run radex and make lib.lvg for z=0 hcn
    # 
    # Usage:
    #     macro read makeliblvg_z_0_hcn_dvddr_1.0_dVDoW_5.0.sm makeliblvg
    # 
    # Note:
    #     please note these:
    #         define TbCMB 6.89  # <TODO> T_{CMB} at z~1.5 see Dannerbauer 2009 BzK
    # 
    # Note: 
    #     additional note:
    #         after making the liblvg, please check the lib.lvg, 
    #         in case there are some ' *'s inside, 
    #         we can replace ' *' with '0'
    # 
    set dirMKLIB = {"/Users/dliu/Temp/spirelines20150330-makeliblvg/02_Make_Lib_LVG"}
    set dirRADEX = {"/Users/dliu/Temp/spirelines20150330-makeliblvg/01_Radex"}
    # 
    set Tk = 5.0,500.,25
    set nH = 2.0,6.5,0.1
    # 
    !echo -n "Will make "$(dimen(Tk)*dimen(nH)*16)' items for lib.lvg, '
    define OK ?
    if(!$OK){
        echo Abort by user. 
        return
    }
    # 
    # constants 
    set k_B = 1.380658e-16 # Boltzmann constant CGS erg K-1
    set h_0 = 6.6260755e-27 # Planck constant CGS erg s
    set v_c = 2.99792458e10 # cm/s
    set g_0 = 6.672e-11
    set tkc = 2.0*k_B/v_c/v_c
    # 
    CHDIR $(dirRADEX)
    !date +"\#\ %Y-%m-%d\ %H:%M:%S" > "lib.lvg"
    print +"lib.lvg" '\# NVAR  = 2 \# Variable X Y\n' {}
    print +"lib.lvg" '\# TVAR1 = J_{upper}\n' {}
    print +"lib.lvg" '\# TVAR2 = S_{HCN}*(1+z)^3/Ω_S, in unit of Jy km/s ' {}
    print +"lib.lvg" '\# Ω_S == π * r_S^2 / d_A^2, in unit of sr\n' {}
    print +"lib.lvg" '\# CVAR1 = 1\n' {} # column number of VAR1
    print +"lib.lvg" '\# CVAR2 = 11\n' {} # column number of VAR2
    print +"lib.lvg" '\# NVAR1 = 6 \# HCN J=6-5 to 1-0\n' {}
    print +"lib.lvg" '\# NVAR2 = $(dimen(Tk)*dimen(nH)) \# \n' {}
    print +"lib.lvg" '\# NPAR  = 2 \# Parameter 1 2 3 ...\n' {}
    print +"lib.lvg" '\# TPAR1 = T_{kin}\n' {}
    print +"lib.lvg" '\# TPAR2 = n_{H_2}\n' {}
    print +"lib.lvg" '\# NPAR1 = $(dimen(Tk))\n' {}
    print +"lib.lvg" '\# NPAR2 = $(dimen(nH))\n' {}
    print +"lib.lvg" '\# CPAR1 = 3\n' {} # column number of PAR1
    print +"lib.lvg" '\# CPAR2 = 4\n' {} # column number of PAR1
    print +"lib.lvg" '\# \n' {}
    print +"lib.lvg" '\# J_upper  J_lower      T_Kin            n_H_2    Null1    Null2    Null3' {}
    print +"lib.lvg" '              tau             T_ex             T_RJ         S_HCN_1zO\n' {}
    define ii 0
    foreach TkOne Tk {
        foreach nHOne nH {
            define dvddr 1.0   # <TODO> well, not sure!
            define dVDoW 5.0   # <TODO> well, not sure! the line width of galactic dense cores are ~5 km/s see Wu et al. 2010 ApJS
            define NXAbD 2e-8  # <TODO> HCN Abundance, see Seaquist & Frayer 2000 and Wild et al. 1992 X(HCN)=2e-8 for M82
            define NHLoS $($dVDoW/$dvddr*(10**$nHOne)*3.086e18)
            define NXLoS $($NHLoS*$NXAbD)
            define TbCMB 2.72548  # <TODO> T_{CMB} at z~0! Fixsen 2009 ApJ
            print  "inp/hcn.inp" './data/hcn.dat\n' {} # <TODO> hcn.dat
            print +"inp/hcn.inp" './out/hcn_$TkOne"_"$nHOne.out\n' {}
            print +"inp/hcn.inp" '80 600\n' {} # Freq Range -- <TODO> must match NVAR1 <TODO>
            print +"inp/hcn.inp" '$TkOne\n' {} # Tk
            print +"inp/hcn.inp" '2\n' {}
            print +"inp/hcn.inp" 'o-H2\n' {}
            print +"inp/hcn.inp" '$(0.75*(10**$nHOne))\n' {} # 0.75 ortho-H2 see Dannerbauer 2009 BzK
            print +"inp/hcn.inp" 'p-H2\n' {}
            print +"inp/hcn.inp" '$(0.25*(10**$nHOne))\n' {} # 0.25 para-H2 see Dannerbauer 2009 BzK
            print +"inp/hcn.inp" '$TbCMB\n' {} # T_{CMB} at z~1.5 Dannerbauer
           #print +"inp/hcn.inp" '$NHLoS\n' {} # Column H2 Density along Line of Sight -- NHLoS
            print +"inp/hcn.inp" '$NXLoS\n' {} # Column CO Density along Line of Sight -- NXLoS
            print +"inp/hcn.inp" '$dVDoW\n' {} # Velocity Corresponding to Doppler Width -- dVDoW
            print +"inp/hcn.inp" '0\n' {}
            !./bin/radex.exe < inp/hcn.inp > out/hcn_$TkOne"_"$nHOne.log
            # 
            data "out/hcn_"$TkOne"_"$nHOne".out" lines 14 0 read {J_UP 1 J_LOW 2 FREQ 4 T_EX 6 TAU 7 T_RJ 8 FLUX1 11}
            set TkArr = T_RJ*0.0+$TkOne
            set nHArr = T_RJ*0.0+(10**$nHOne)
            set emArr = T_RJ*0.0+0.0
            set S_HCN = T_RJ * tkc * (FREQ*1e9)**2 * 1.0645*$dVDoW * 1e23 # T_RJ * 2 k nu^2/c^2 * DeltaV
                                                                         # in unit of Jy km/s
                                                                         # 1.0645 see radex_manual.pdf sec.4.3
            # 
            define print_noheader 1
            print +"lib.lvg" '%9.0f%9.0f %10g %16e %8g %8g %8g %16e %16e %16e %16e\n' \
                             {J_UP J_LOW TkArr nHArr emArr emArr emArr TAU T_EX T_RJ S_HCN}
        }
    }
    !pwd
    CHDIR $(dirMKLIB)
    !pwd
    # 
    # copy
    !cp $(dirRADEX)/lib.lvg lib_z_0_hcn_dvddr_1.0_dVDoW_5.0.lvg
    # 
    # end
