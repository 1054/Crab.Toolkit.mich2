make_Calzetti2000law_BC03
    #  
    #  macro read make_Calzetti2000law.sm make_Calzetti2000law_BC03
    #  
    #  see formula of Calzetti2000law: 
    #  http://webast.ast.obs-mip.fr/hyperz/hyperz_manual1/node10.html
    #  
    
    data "lib.BC03.Padova1994.BaSeL.Z0.0190.SED"
    read {lambda 1 flux 2 metallicity 3 age 4 mass 5}
    
    # copy original data arrays
    foreach foo {lambda flux metallicity age mass} {set _$foo = $foo}
    set flux_lambda = flux / (lambda/1e6)**2 * 2.99792458e8 # S_{\nu} [Lsun per Hz] to S_{\lambda} [Lsun per m]
    set _flux_lambda = flux_lambda
    
    # we set 3 E(B-V) values
    # we set 9 E(B-V) values, repeat original data array by 9x
    # we set 6 E(B-V) values, repeat original data array by 6x
    set ebminusv =                  lambda*0+0.0
    set ebminusv = ebminusv concat (lambda*0+0.1)
    set ebminusv = ebminusv concat (lambda*0+0.2)
    set ebminusv = ebminusv concat (lambda*0+0.3)
    set ebminusv = ebminusv concat (lambda*0+0.4)
    set ebminusv = ebminusv concat (lambda*0+0.5)
    set ebminusv = ebminusv concat (lambda*0+0.6)
    set ebminusv = ebminusv concat (lambda*0+0.7)
    set ebminusv = ebminusv concat (lambda*0+0.8)
    set ebminusv = ebminusv concat (lambda*0+0.9)
    set ebminusv = ebminusv concat (lambda*0+1.0)
    set ebminusv_numb = 11
    do i=1,ebminusv_numb-1 {
        foreach foo {lambda flux flux_lambda metallicity age mass} {set $foo = $foo concat _$foo}
    }
    
    # <TODO> extended wavelength range
    # set k_lambda = (lambda>=0.12 && lambda<0.63) ? 2.659*(-2.156+1.509/lambda-0.198/lambda**2+0.011/lambda**3)+4.05 : 0.0
    # set k_lambda = (lambda>=0.63 && lambda<2.20) ? 2.659*(-1.857+1.040/lambda)+4.05 : k_lambda
    # <TODO> extended wavelength range
    set k_lambda = (lambda>=0.00 && lambda<0.63) ? 2.659*(-2.156+1.509/lambda-0.198/lambda**2+0.011/lambda**3)+4.05 : 0.0
    set k_lambda = (lambda>=0.63 && lambda<=1e4) ? 2.659*(-1.857+1.040/lambda)+4.05 : k_lambda
    set k_lambda = (k_lambda<0.0) ? 0.0 : k_lambda
    set A_lambda = ebminusv * k_lambda
    set B_lambda = 10**(-0.4*A_lambda)
    set f_lambda = flux_lambda * B_lambda
    set f_nu = f_lambda * (lambda/1e6)**2 / 2.99792458e8
    set f_nu = f_nu<1e-30 ? 1e-30 : f_nu   #<TODO># make sure than lib.SED does not contain 0 flux!
    set flux = f_nu
    
    # write new lib file
    define outputf "lib.BC03.Padova1994.BaSeL.Z0.0190.EBV.SED"
    define print_noheader 1
    !echo "\\# "\$(date) > $outputf
    print +$outputf '\# NVAR  = 2     \# Wave & Flux\n' {}
    print +$outputf '\# TVAR1 = Wave  \# band wavelength (rest-frame) (lambda) [um]\n' {}
    print +$outputf '\# TVAR2 = Flux  \# flux density in unit of solar luminosity per Hz' {}
    print +$outputf ' | (VAR2*3.839e33*1e26/(4*pi*dL**2*9.52140e48)/(1+z)) becomes [mJy], dL=[Mpc].\n' {}
    print +$outputf '\# CVAR1 = 1     \# the colomn number of VAR1\n' {}
    print +$outputf '\# CVAR2 = 2     \# the colomn number of VAR2\n' {}
    print +$outputf '\# NVAR1 = 2023  \# NAXIS1 -- the number of wavelength in one spectrum\n' {}
    print +$outputf '\# NVAR2 = $(ebminusv_numb)     \# NAXIS2 -- the number of spectra -- product of all indepedent PAR\n' {} # now 1 Age x ebminusv_numb EBV
    print +$outputf '\# NPAR  = 4     \# Metallicity, Age, Mass (locked to Age), EBV\n' {}
    print +$outputf '\# TPAR1 = Metal \# Absolute Metallicity (note that solar metallicity is 0.0190)\n' {}
    print +$outputf '\# TPAR2 = Age   \# Age in unit of Gyr\n' {}
    print +$outputf '\# TPAR3 = Mass  \# Stellar Mass in unit of solar mass (associated with Age, not independent)\n' {}
    print +$outputf '\# TPAR4 = EBV   \# E(B-V)\n' {}
    print +$outputf '\# CPAR1 = 3     \# the colomn number of PAR1, Metallicity\n' {}
    print +$outputf '\# CPAR2 = 4     \# the colomn number of PAR2, Age\n' {}
    print +$outputf '\# CPAR3 = 5     \# the colomn number of PAR3, Mass\n' {}
    print +$outputf '\# CPAR4 = 6     \# the colomn number of PAR4, EBV\n' {}
    print +$outputf '\# NPAR1 = 1     \# Number of different PAR1, Metallicity\n' {}
    print +$outputf '\# NPAR2 = 1     \# Number of different PAR2, Age\n' {}
    print +$outputf '\# NPAR3 = 1     \# Number of different PAR3, Mass\n' {}
    print +$outputf '\# NPAR4 = $(ebminusv_numb)     \# Number of different PAR4, EBV\n' {}
    print +$outputf '\# \n' {}
    print +$outputf '\#        lambda           flux    metallicity         age           mass         ebv\n' {}
    print +$outputf '\#          [um]      [Lsun/Hz]                      [Gyr]         [Msun]            \n' {}
    print +$outputf '%15.6e%15.6e%15.5f%12.5f%15.6e%12g\n' {lambda flux metallicity age mass ebminusv}
    define print_noheader 0
    
    #set __flux = _flux concat _flux
    #set __flux = __flux concat _flux
    #print "test.txt" {lambda A_lambda B_lambda __flux flux}


